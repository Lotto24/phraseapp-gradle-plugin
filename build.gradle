/*
 * Copyright 2016 Mike Winkelmann <mike.winkelmann@esailors.de>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
    }
}

// Apply the java plugin to add support for ...
plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.6"
}

group = 'de.esailors.gradle.plugins'
version = '1.0.1-SNAPSHOT'
description = 'Synchronizing your PhraseApp strings for a configured project.'
project.ext.baseVersion = project.version.replace("-SNAPSHOT", "")

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    compile 'org.projectlombok:lombok:1.16.10'
    compile 'com.mytaxi.apis:phrase-java-client:1.0.1'
    compile 'org.springframework:spring-web:3.2.15.RELEASE'
    testCompile 'junit:junit:4.12'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    exclude "**/internal/**"
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    title "PhraseApp Gradle plugin"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task publishLocal(type: Upload) {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: uri('repo'))
        }
    }
}

pluginBundle {

    website = 'http://www.gradle.org/'
    vcsUrl = 'https://github.com/eSailors/phraseapp-gradle-plugin'

    plugins {
        phraseAppPlugin {
            id = 'de.esailors.gradle.plugins.phraseapp'
            displayName = 'PhraseApp-Gradle-Plugin'
            description = project.description
            tags = ['esailors', 'phraseapp', 'phrase', 'i18n', 'translation', 'plugin']
            version = project.ext.baseVersion
        }
    }
}
